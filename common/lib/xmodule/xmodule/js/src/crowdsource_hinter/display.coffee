class @Hinter

  constructor: (element) ->
    @el = $(element).find('.crowdsource-wrapper')
    @url = @el.data('url')
    Logger.listen('problem_graded', @el.data('child-url'), @capture_problem)
    # The line below will eventually be generated by Python.
    @render()

  capture_problem: (event_type, data, element) =>
    # After a problem gets graded, we get the info here.
    # We want to send this info to the server in another AJAX
    # request.
    answers = data[0]
    response = data[1]
    if response.search(/class="correct "/) == -1
      # Incorrect.  Get hints.
      $.postWithPrefix "#{@url}/get_hint", answers, (response) =>
        @render(response.contents)
    else
      # Correct.  Get feedback from students.
      $.postWithPrefix "#{@url}/get_feedback", answers, (response) =>
        @render(response.contents)

  $: (selector) ->
    $(selector, @el)

  bind: =>
    window.update_schematics()
    @$('input.vote').click @vote
    @$('#feedback-select').change @feedback_ui_change
    @$('input.submit-hint').click @submit_hint


  vote: (eventObj) =>
    target = @$(eventObj.currentTarget)
    post_json = {'answer': target.data('answer'), 'hint': target.data('hintno')}
    $.postWithPrefix "#{@url}/vote", post_json, (response) =>
      @render(response.contents)

  submit_hint: (eventObj) =>
    target = @$(eventObj.currentTarget)
    textarea_id = '#custom-hint-' + target.data('answer')
    console.debug(textarea_id)
    post_json = {'answer': target.data('answer'), 'hint': @$(textarea_id).val()}
    $.postWithPrefix "#{@url}/submit_hint",post_json, (response) =>
      @render(response.contents)

  feedback_ui_change: =>
    # Make all of the previous-answer divs hidden.
    @$('.previous-answer').css('display', 'none')
    # But, now find the selected div, and make it visible.
    selector = '#previous-answer-' + @$('#feedback-select option:selected').attr('value')
    @$(selector).css('display', 'inline')


  render: (content) ->
    if content
      @el.html(content)
      JavascriptLoader.executeModuleScripts @el, () =>
        @bind()
      @$('#previous-answer-0').css('display', 'inline')